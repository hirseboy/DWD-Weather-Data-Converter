[Setup]
Compression=lzma
SolidCompression=true
OutputDir=.
UsePreviousAppDir=false
UsePreviousGroup=false
InternalCompressLevel=ultra

AppName=DWDWeatherDataConverter 64-Bit
AppVerName=DWDWeatherDataConverter 0.9 64-Bit
AppVersion=0.99
DefaultDirName={pf}\IBK\DWDWeatherDataConverter 0.9
DefaultGroupName=IBK\DWDWeatherDataConverter 0.9 64-Bit
OutputBaseFilename=DWDWeatherDataConverter_win64
VersionInfoVersion=0.9.9
VersionInfoDescription=DWDWeatherDataConverter 64-Bit
UninstallDisplayIcon=DWDWeatherDataConverter\DWDWeatherDataConverter.exe

AppPublisher=Bauklimatik Dresden Software GmbH
AppPublisherURL=https://www.bauklimatik-dresden.de
AppSupportURL=https://www.bauklimatik-dresden.de/schulungen.php
AppUpdatesURL=https://www.bauklimatik-dresden.de/downloads.php
VersionInfoCompany=Bauklimatik Dresden Software GmbH
VersionInfoCopyright=2023 Bauklimatik Dresden Software GmbH
AppCopyright=2023 Bauklimatik Dresden Software GmbH
ArchitecturesInstallIn64BitMode=x64


[Files]
; Program files
Source: ..\..\bin\release_x64\DWDWeatherDataConverter.exe; DestDir: {app}; DestName: DWDWeatherDataConverter.exe

; Dependencies according to DependencyWalker
Source: VC19_x64\Qt5Svg.dll; DestDir: {app}
Source: VC19_x64\Qt5Network.dll; DestDir: {app}
Source: VC19_x64\Qt5Widgets.dll; DestDir: {app}
Source: VC19_x64\Qt5Gui.dll; DestDir: {app}
Source: VC19_x64\Qt5Core.dll; DestDir: {app}

Source: VC19_x64\msvcp140.dll; DestDir: {app}
Source: VC19_x64\vcomp140.dll; DestDir: {app}
Source: VC19_x64\vcruntime140_1.dll; DestDir: {app}
Source: VC19_x64\vcruntime140.dll; DestDir: {app}

Source: VC19_x64\bearer\*; DestDir: {app}\bearer
Source: VC19_x64\iconengines\*; DestDir: {app}\iconengines
Source: VC19_x64\imageformats\*; DestDir: {app}\imageformats
Source: VC19_x64\platforms\qwindows.dll; DestDir: {app}\platforms
Source: VC19_x64\styles\*; DestDir: {app}\styles
Source: VC19_x64\printsupport\*; DestDir: {app}\printsupport

; Files for OpenSSL support (i.e. when accessing https://bauklimatik-dresden.de)
Source: VC19_x64\libcrypto-1_1-x64.dll; DestDir: {app}
Source: VC19_x64\libssl-1_1-x64.dll; DestDir: {app}

; Translation files
Source: ..\..\DWDWeatherDataConverter\resources\translations\*.qm; DestDir: {app}\resources\translations

; Example Files
Source: ..\..\data\examples\*; DestDir: {app}\resources\examples; Flags: recursesubdirs createallsubdirs

[InstallDelete]
Name: {app}; Type: filesandordirs

[UninstallDelete]
Name: {app}; Type: filesandordirs

[Icons]
Name: {group}\DWDWeatherDataConverter 0.9 64-Bit; Filename: {app}\DWDWeatherDataConverter.exe; WorkingDir: {app}; IconIndex: 0; Flags: runmaximized
;Name: {group}\DWDWeatherDataConverter 0.9 64-Bit Online Benutzerhandbuch (deutsch); Filename: "https://bauklimatik-dresden.de/DWDWeatherDataConverter/documentation.php"

[Run]
Filename: {app}\DWDWeatherDataConverter.exe; WorkingDir: {app}; Flags: postinstall unchecked; Description: "Starte DWDWeatherDataConverter 0.9"; Languages: Deutsch
Filename: {app}\DWDWeatherDataConverter.exe; WorkingDir: {app}; Flags: postinstall unchecked; Description: "Run DWDWeatherDataConverter 0.9"; Languages: English

[Registry]
Root: HKCR; Subkey: .vicus; ValueType: string; ValueData: DWDWeatherDataConverter.Project; Flags: uninsdeletekey deletekey
Root: HKCR; Subkey: DWDWeatherDataConverter.Project; ValueType: string; ValueData: DWDWeatherDataConverter Project File; Flags: uninsdeletekey deletekey
Root: HKCR; Subkey: DWDWeatherDataConverter.Project\shell\open\command; ValueType: string; ValueData: """{app}\DWDWeatherDataConverter.exe"" ""%1"""; Flags: uninsdeletekey deletekey

[Languages]
Name: English; MessagesFile: compiler:Default.isl
Name: Deutsch; MessagesFile: compiler:Languages\German.isl
